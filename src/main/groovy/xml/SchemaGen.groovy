package xml

import org.gradle.api.DefaultTask
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.InputFile
import org.gradle.api.tasks.TaskAction

class SchemaGen extends DefaultTask {
    /**
     * The file representing the Java class from which to generate the XML Schema
     */
    @InputFile File schemaGenClass
    /**
     * The destination folder for the generated XML Schema
     */
    @Input String destDir
    /**
     * The Java bytecode files required on the classpath.
     * <p>Typically this will be buildDir/classes/main.
     */
    @Input String classPath
    /**
     * A name for the generated XML Schema
     */
    @Input String xsdFileName
    /**
     * A string specifying the schemagen command.
     * <p>Note: schemagen should be included in the JDK which
     * should be in the system path environment variable.
     */
    private final String command = "cmd /c schemagen"

    SchemaGen() {
        group = "documentation"
        description = "Create an XML Schema document from a Java class"
    }

    @TaskAction
    void start() {
        project.file(destDir).mkdirs()
        /**
         * The entire command to execute, including all input fields for the task
         */
        final String run = "${command} -cp ${classPath} ${schemaGenClass.absolutePath} -d ${destDir}"
        /**
         * Execute the schemagen command
         */
        println run.execute().text
        /**
         * Use the Ant Move task to rename the schema1.xsd file generated by default
         */
        if(project.file("${destDir}/schema1.xsd").exists())
            project.ant.move(file: project.file("${destDir}/schema1.xsd"), tofile: project.file("${destDir}/${xsdFileName}"))
    }
}
